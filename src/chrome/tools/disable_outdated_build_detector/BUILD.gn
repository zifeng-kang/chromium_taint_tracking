# Copyright (c) 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")
import("//testing/test.gni")

if (is_win) {
  executable("disable_outdated_build_detector") {
    sources = [
      "disable_outdated_build_detector_main.cc",
    ]

    configs -= [ "//build/config/win:console" ]
    configs += [ "//build/config/win:windowed" ]

    deps = [
      ":disable_outdated_build_detector_exe_version",
      ":lib",
      "//base",
      "//build/win:default_exe_manifest",
    ]
  }

  process_version("disable_outdated_build_detector_exe_version") {
    template_file = "disable_outdated_build_detector_exe_version.rc.version"
    output = "$target_gen_dir/disable_outdated_build_detector_exe_version.rc"
  }

  source_set("lib") {
    sources = [
      "constants.cc",
      "constants.h",
      "disable_outdated_build_detector.cc",
      "disable_outdated_build_detector.h",
      "google_update_integration.cc",
      "google_update_integration.h",
    ]

    public_deps = [
      "//base",
    ]
  }

  test("disable_outdated_build_detector_unittests") {
    sources = [
      "disable_outdated_build_detector_unittest.cc",
      "google_update_integration_unittest.cc",
      "run_all_unittests.cc",
    ]

    deps = [
      ":lib",
      "//base",
      "//base/test:test_support",
      "//chrome/installer/util:with_no_strings",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}
